import java.nio.file.Paths

buildscript {
  repositories {
    google()
    mavenCentral()
  }

  dependencies {
    classpath "com.android.tools.build:gradle:7.2.2"
  }
}

def isNewArchitectureEnabled() {
  return rootProject.hasProperty("newArchEnabled") && rootProject.getProperty("newArchEnabled") == "true"
}

apply plugin: "com.android.library"


def appProject = rootProject.allprojects.find { it.plugins.hasPlugin('com.android.application') }

if (isNewArchitectureEnabled()) {
  apply plugin: "com.facebook.react"
}

def getExtOrDefault(name) {
  return rootProject.ext.has(name) ? rootProject.ext.get(name) : project.properties["Tflite_" + name]
}

def getExtOrIntegerDefault(name) {
  return rootProject.ext.has(name) ? rootProject.ext.get(name) : (project.properties["Tflite_" + name]).toInteger()
}

static def findNodeModules(baseDir) {
  def basePath = baseDir.toPath().normalize()
  // Node's module resolution algorithm searches up to the root directory,
  // after which the base path will be null
  while (basePath) {
    def nodeModulesPath = Paths.get(basePath.toString(), "node_modules")
    def reactNativePath = Paths.get(nodeModulesPath.toString(), "react-native")
    if (nodeModulesPath.toFile().exists() && reactNativePath.toFile().exists()) {
      return nodeModulesPath.toString()
    }
    basePath = basePath.getParent()
  }
  throw new GradleException("react-native-fast-tflite: Failed to find node_modules/ path!")
}

def nodeModules = findNodeModules(projectDir)

android {
  ndkVersion getExtOrDefault("ndkVersion")
  compileSdkVersion getExtOrIntegerDefault("compileSdkVersion")
  namespace "com.tflite"

  defaultConfig {
    minSdkVersion getExtOrIntegerDefault("minSdkVersion")
    targetSdkVersion getExtOrIntegerDefault("targetSdkVersion")
    buildConfigField "boolean", "IS_NEW_ARCHITECTURE_ENABLED", isNewArchitectureEnabled().toString()
    externalNativeBuild {
      cmake {
        arguments "-DANDROID_STL=c++_shared",
                "-DNODE_MODULES_DIR=${nodeModules}",
                "-DIS_NEW_ARCHITECTURE_ENABLED=${isNewArchitectureEnabled()}",
                "-DANDROID_SUPPORT_FLEXIBLE_PAGE_SIZES=ON"
        cppFlags "-O2 -frtti -fexceptions -Wall -fstack-protector-all"
        abiFilters "x86", "x86_64", "armeabi-v7a", "arm64-v8a"
      }
    }
  }
  externalNativeBuild {
    cmake {
      path "CMakeLists.txt"
    }
  }
  packagingOptions {
    excludes = [
            "META-INF",
            "META-INF/**",
            "**/libc++_shared.so",
            "**/libfbjni.so",
            "**/libjsi.so",
            "**/libreactnative.so",
            "**/libreactnativejni.so",
            "**/libturbomodulejsijni.so",
            "**/libreact_nativemodule_core.so",
            "**/libtensorflowlite_jni.so",
            "**/libtensorflowlite_gpu_jni.so",
    ]
  }
  buildTypes {
    release {
      minifyEnabled false
    }
  }

  buildFeatures {
    prefab true
  }

  lintOptions {
    disable "GradleCompatible"
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  configurations {
    extractHeaders
    extractSO
  }
}

repositories {
  mavenCentral()
  google()
}


dependencies {
  // For < 0.71, this will be from the local maven repo
  // For > 0.71, this will be replaced by `com.facebook.react:react-android:$version` by react gradle plugin
  //noinspection GradleDynamicVersion
  implementation "com.facebook.react:react-native:+"

  // Tensorflow Lite .aar (includes C API via prefabs)
  implementation "com.google.ai.edge.litert:litert:1.4.0"
  extractSO("com.google.ai.edge.litert:litert:1.4.0")
  extractHeaders("com.google.ai.edge.litert:litert:1.4.0")

  // Tensorflow Lite GPU delegate
  implementation "com.google.ai.edge.litert:litert-gpu:1.4.0"
  extractSO("com.google.ai.edge.litert:litert-gpu:1.4.0")
  extractHeaders("com.google.ai.edge.litert:litert-gpu:1.4.0")
}

/**
 * Custom task to delete the 'src/main/cpp/lib/headers' directory.
 */
task cleanEmptyDirectories(type: Delete) {
  // Specify the directory to be deleted.
  delete 'src/main/cpp/lib/headers'
  delete 'src/main/cpp/lib/res'
}

/**
 * Custom task to copy C++ header files (.h) from AAR packages.
 * This task iterates through each AAR file in the 'extractHeaders' configuration.
 * For each AAR, it extracts and copies header files to specific destinations:
 * - Headers found under 'external/org_tensorflow/tensorflow/' inside the AAR
 * are copied to 'src/main/cpp/lib/tensorflow/'.
 * - All other headers are copied to 'src/main/cpp/lib/{packageName}/', where
 * '{packageName}' is derived from the AAR's filename.
 */
task extractAARHeaders {

  finalizedBy cleanEmptyDirectories

  doLast {
    // Iterate over each AAR file defined in the 'extractHeaders' configuration
    configurations.extractHeaders.files.each { aarFile ->
      // Extract the package name from the AAR filename (e.g., 'my-lib-1.0.aar' -> 'my-lib')
      def packageName = aarFile.name.tokenize('-')[0]

      // Create a copy operation for the current AAR
      copy {
        // Source: the contents of the AAR treated as a zip archive
        from zipTree(aarFile)
        // Base destination directory for all copied files from this AAR.
        // The 'eachFile' closure will then modify the relative path within this base.
        into "src/main/cpp/lib/"
        include "**/*.h" // Ensure only header files are included

        // Process each file found within the AAR's zipTree
        eachFile { fileCopyDetails ->
          // Check if the current file is a C++ header file (.h)
          if (fileCopyDetails.name.endsWith(".h")) {
            // Get the original relative path of the file within the AAR
            def originalRelativePath = fileCopyDetails.relativePath.toString()

            // Check if the header belongs to the special TensorFlow path
            if (originalRelativePath.startsWith("headers/external/org_tensorflow/tensorflow/")) {
              // For TensorFlow headers, set the new relative path to start with 'tensorflow/'
              // and remove the 'external/org_tensorflow/' prefix.
              def newRelativePath = packageName + "/headers/" + originalRelativePath.substring("headers/external/org_tensorflow/".length())
              fileCopyDetails.relativePath = new RelativePath(true, newRelativePath.split('/'))
            } else {
              // For all other headers, set the new relative path to include the 'packageName'
              // derived from the AAR filename, followed by its original path.
              def newRelativePath = packageName + "/" + originalRelativePath
              fileCopyDetails.relativePath = new RelativePath(true, newRelativePath.split('/'))
            }
          } else {
            // If the file is not a .h file, exclude it from the copy operation
            fileCopyDetails.exclude()
          }
        }
      }
    }
  }
}

task extractSOFiles {
  doLast {
    configurations.extractSO.files.each {
      def file = it.absoluteFile
      def packageName = file.name.tokenize('-')[0]
      copy {
        from zipTree(file)
        into "src/main/cpp/lib/$packageName/"
        include "jni/**/*.so"
      }
    }
  }
}

if (isNewArchitectureEnabled()) {
  react {
    jsRootDir = file("../src/")
    libraryName = "Tflite"
    codegenJavaPackageName = "com.tflite"
  }
}


def nativeBuildDependsOn(dependsOnTask) {
  def buildTasks = tasks.findAll({ task -> (
          !task.name.contains("Clean")
                  && (task.name.contains("externalNative")
                  || task.name.contains("CMake")
                  || task.name.contains("generateJsonModel")
          )
  ) })
  buildTasks.forEach { task -> task.dependsOn(dependsOnTask) }
}

afterEvaluate {
  nativeBuildDependsOn(extractAARHeaders)
  nativeBuildDependsOn(extractSOFiles)
}
